plugins {
    id "java"
    id "jacoco"
    id "org.springframework.boot" version "3.4.0"
    id "io.spring.dependency-management" version "1.1.6"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id "com.google.cloud.tools.jib" version "3.4.4"
    id "com.diffplug.spotless" version "6.25.0"
    id "org.sonarqube" version "6.0.1.5171"
}

group = "com.sivalabs"
version = "0.0.1"
archivesBaseName = "devzone"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def dockerImageName = "sivaprasadreddy/devzone"
def dockerImageTags = ["latest", "${project.version}"]
def dockerBaseImage = "eclipse-temurin:21-jre"
def appMainClass = "com.sivalabs.devzone.Application"
def jacocoToolVersion = "0.8.12"
def jacocoMinCoverageRequired = 0.30
def jacocoExcludes = [
    "com/sivalabs/devzone/*Application.*",
]

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.thymeleaf.extras:thymeleaf-extras-springsecurity6"
    implementation "nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect"

    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0"
    implementation "com.opencsv:opencsv:5.9"
    implementation "org.jsoup:jsoup:1.18.3"
    implementation "org.apache.commons:commons-lang3"
    implementation "commons-io:commons-io:2.18.0"
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-database-postgresql"
    runtimeOnly "io.micrometer:micrometer-registry-prometheus"
    runtimeOnly "io.micrometer:micrometer-tracing-bridge-otel"
    runtimeOnly "io.opentelemetry:opentelemetry-exporter-zipkin"
    implementation "net.ttddyy.observation:datasource-micrometer-spring-boot:1.0.6"

    runtimeOnly "org.postgresql:postgresql"
    //implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.10.0"

    implementation "org.webjars:webjars-locator-lite"
    implementation "org.webjars:bootstrap:5.3.3"
    implementation "org.webjars:jquery:3.7.1"
    implementation "org.webjars:font-awesome:6.7.1"

    developmentOnly "org.springframework.boot:spring-boot-devtools"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    developmentOnly "org.springframework.boot:spring-boot-docker-compose"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.springframework.security:spring-security-test"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"
    testImplementation "com.tngtech.archunit:archunit-junit5:1.3.0"
    testImplementation "com.apptasticsoftware:rssreader:3.9.1"
}

bootBuildImage {
    imageName = dockerImageName
    tags = dockerImageTags
}

jib {
    from {
        image = dockerBaseImage
    }
    to {
        image = dockerImageName
        tags = dockerImageTags
    }
    container {
        jvmFlags = ["-Xms512m", "-Xdebug"]
        mainClass = appMainClass
        args = []
        ports = ["8080/tcp"]
    }
}

if (project.hasProperty("dev")) {
    bootRun {
        args = ["--spring.profiles.active=dev"]
    }
}

gitProperties {
    failOnNoGitDirectory = false
    keys = ["git.branch",
            "git.commit.id.abbrev",
            "git.commit.user.name",
            "git.commit.message.full"]
}

spotless {
    java {
        palantirJavaFormat('2.50.0')
        importOrder()
        removeUnusedImports()
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events = [/*"PASSED",*/ "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

test.dependsOn(spotlessCheck)

jacoco {
    toolVersion = jacocoToolVersion
}

jacocoTestReport {
    executionData tasks.withType(Test)
    classDirectories.from = files(sourceSets.main.output.classesDirs)
    sourceDirectories.from = files(sourceSets.main.java.srcDirs)

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        }))
    }

    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn "test"
    executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")

    violationRules {
        rule {
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = jacocoMinCoverageRequired
            }
            excludes = jacocoExcludes
        }
    }
}

check.dependsOn jacocoTestReport, jacocoTestCoverageVerification

file("sonar-project.properties").withReader {
    Properties sonarProperties = new Properties()
    sonarProperties.load(it)

    sonarProperties.each { key, value ->
        sonarqube {
            properties {
                property key, value
            }
        }
    }
}
